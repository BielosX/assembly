image := "aarch64-binutils:latest"
podman-binutils-cmd := "podman run --rm -v $(pwd):/usr/src/workdir -w /usr/src/workdir " + image
as := podman-binutils-cmd + " aarch64-linux-gnu-as"
ld := podman-binutils-cmd + " aarch64-linux-gnu-ld"
ld-flags := "-nostdlib"
as-flags := "-I include"

build-image:
    podman build -t {{image}} {{justfile_directory()}}
    podman image prune --force --filter label=stage=builder

check-image:
    podman image exists {{image}}

hello-world: check-image
    mkdir -p bin
    {{as}} {{as-flags}} -o bin/hello_world.o hello_world/main.S
    {{ld}} {{ld-flags}} -o bin/hello_world bin/hello_world.o

base64encode: check-image
    mkdir -p bin
    {{as}} {{as-flags}} -o bin/base64encode.o base64encode/main.S
    {{ld}} {{ld-flags}} -o bin/base64encode bin/base64encode.o

clean:
    rm -rf bin
